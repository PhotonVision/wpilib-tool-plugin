import org.gradle.internal.os.OperatingSystem;

plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "1.3.0"
}

group 'org.photonvision'
version '2.1.0-photon'

if (project.hasProperty('publishVersion')) {
    version = project.publishVersion
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

gradlePlugin {
    website = 'https://github.com/photonvision/wpilib-tool-plugin'
    vcsUrl = 'https://github.com/photonvision/wpilib-tool-plugin'

    plugins {
        WpilibTools {
            id = 'org.photonvision.tools.WpilibTools'
            implementationClass = 'org.photonvision.tools.WpilibTools'
            displayName = 'WPILib Gradle Tool Plugin'
            description = 'This plugin provides easy to use JNI support for gradle.'
            tags = ['groovy', 'jni', 'utils', 'maven', 'frc', 'wpilib', 'photonvision']
        }
    }
}

tasks.withType(Javadoc) {
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

wrapper {
    gradleVersion = '8.5'
}

publishing {
    repositories {
        maven {
            name = "PhotonVision"
            url = "https://maven.photonvision.org/releases"
            credentials {
                username = "ghactions"
                password = System.getenv("ARTIFACTORY_API_KEY")
            }
        }
    }
}
